buildscript {
  dependencies {
    classpath dep.androidPlugin
  }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
  compileSdkVersion 29
  buildToolsVersion "30.0.0"
}

task androidJavaDoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
  options.encoding "UTF-8"
  options.charSet 'UTF-8'
  options.author true
  options.version true
  failOnError false
}

task androidJavaDocJar(type: Jar, dependsOn: androidJavaDoc) {
  classifier = 'javadoc'
  from androidJavaDoc.destinationDir
}

task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.srcDirs
}

publishing {
  publications {
    aar(MavenPublication) {
      groupId = 'com.squareup.sqldelight'
      artifactId = 'runtime'
      version = '0.6.2'

      afterEvaluate {
        artifact bundleReleaseAar
        artifact androidJavaDocJar
        artifact androidSourcesJar
      }
    }
  }
}

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  lintOptions {
    textOutput 'stdout'
  }

  dexOptions {
    preDexLibraries = !rootProject.ext.isCi
  }

  defaultConfig {
    minSdkVersion 4
  }
}

dependencies {
  compile dep.supportAnnotations

  testCompile dep.junit
  testCompile dep.okio
}

configurations {
  jar
}
android.libraryVariants.all { variant ->
  if (variant.name != 'release') return

  def task = project.tasks.create "jar${variant.name.capitalize()}", Jar
  task.description = "Create jar artifact for ${variant.name}"
  task.dependsOn variant.javaCompile
  task.from variant.javaCompile.destinationDir
  task.destinationDir = project.file("${project.buildDir}/outputs/jar")
  task.archiveName = "${project.name}-${variant.baseName}.jar"
  artifacts.add('jar', task);
}

//apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
