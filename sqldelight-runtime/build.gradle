buildscript {
  dependencies {
    classpath 'com.android.tools.build:gradle:3.1.2'
  }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
  compileSdkVersion 29
  buildToolsVersion "30.0.0"

  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 29
    versionCode 1
    versionName "0.1.0"
  }
}

dependencies {
  implementation 'androidx.appcompat:appcompat:1.0.2'
  implementation 'androidx.annotation:annotation:1.0.2'

  testImplementation'junit:junit:4.12'
  testImplementation 'com.squareup.okio:okio:1.11.0'
}

task androidJavaDoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
  options.encoding "UTF-8"
  options.charSet 'UTF-8'
  options.author true
  options.version true
  failOnError false
}

task androidJavaDocJar(type: Jar, dependsOn: androidJavaDoc) {
  classifier = 'javadoc'
  from androidJavaDoc.destinationDir
}

task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
      aar(MavenPublication) {
        groupId = 'com.squareup.sqldelight'
        artifactId = 'runtime'
        version = '0.6.2'
        afterEvaluate {
          artifact bundleReleaseAar
          artifact androidJavaDocJar
          artifact androidSourcesJar


          pom.withXml {
            def rootNode = asNode().appendNode('dependencies')

            /**
             * Helper method to add dependencies to the POM node
             */
            ext.addDependency = { groovy.util.Node dependenciesNode, Dependency dependency, String dependencyScope ->
              // We don't add incomplete dependencies
              if (dependency.name == null ||
                      dependency.name == 'unspecified' ||
                      dependency.group == null ||
                      dependency.version == null) {
                return
              }

              def dependencyNode = dependenciesNode.appendNode('dependency')
              dependencyNode.appendNode('groupId', dependency.group)
              dependencyNode.appendNode('artifactId', dependency.name)
              dependencyNode.appendNode('version', dependency.version)
              dependencyNode.appendNode('scope', dependencyScope)
            }

            // Iterate over the implementation dependencies, adding a <dependency> node for each
            configurations.implementation.dependencies.each {
              addDependency(rootNode, it, "runtime")
            }

            // Iterate over the api dependencies, adding a <dependency> node for each
            configurations.api.dependencies.each {
              addDependency(rootNode, it, "compile")
            }
          }
      }
    }
  }
}

//android.libraryVariants.all { variant ->
//  if (variant.name != 'release') return
//
//  def task = project.tasks.create "jar${variant.name.capitalize()}", Jar
//  task.description = "Create jar artifact for ${variant.name}"
//  task.dependsOn variant.javaCompile
//  task.from variant.javaCompile.destinationDir
//  task.destinationDir = project.file("${project.buildDir}/outputs/jar")
//  task.archiveName = "${project.name}-${variant.baseName}.jar"
//  artifacts.add('jar', task);
//}

//apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
